# EDHEC Introduction to Portfolio Construction and Analysis

This article is authored by [Alexander Unterrainer](https://www.linkedin.com/in/alexanderunterrainer/), a KDB/Q consultant and founder of 
[DefconQ](https://www.defconq.tech), a blog dedicated to helping others master this powerful programming language. 
The tutorial builds on the lab exercises from the [Coursera](https://www.coursera.org) course 
[*Introduction to Portfolio Construction and Analysis with Python*](https://www.coursera.org/learn/introduction-portfolio-construction-python) offered by 
[EDHEC Business School](https://www.edhec.edu/en), adapted to KDB/Q to highlight its efficiency.
In this guide, you'll learn to calculate basic returns, measure risk and reward, compute maximum drawdown, and much more. You can learn more about KDB/Q by
visiting www.defconq.tech and subscribing to the freee newsletter [here](https://defconq.substack.com)

## Basic Returns

First, we will calculate the basic return of a stock price. Remeber the return of a stock can be calcualted as follows

`R(t) = (P(t)-P(t-1))/P(t-1)`
or
`R(t)=(P(t)/P(t-1))-1`

You can leverage the KDB/Q operator `ratios` for that 

```sql showcodeonly
// Note we have to remove the first element using 1_ as there is no previous element
q)-1+1_ratios 8 9 10
0.125 0.1111111
```

Next we can import some sample prices and calculate the returns

```sql showcodeonly
prices:("FF";enlist ",") 0: `$":sample_prices.csv"
```

```sql type="grid" server="localhost"
prices:("FF";enlist ",") 0: `$":sample_prices.csv";
prices
```

Returns

```sql type="grid" server="localhost"
-1+1_ratios prices
```

We can also draw the prices in a line chart

```sql type="line" server="localhost"
prices
```

And the returns in a bar chart 

```sql type="bar" server="localhost"
1_-1+ratios prices
```

Let's calculate the mean and standard deviation for both time series. The inbuild `avg` and `sdev` operators make this calcuation straight forward

```sql showcodeonly
select meanBlue:avg BLUE,StandardDeviationBlue:sdev BLUE,
        meanOrange:avg ORANGE,StandardDeviationOrange:sdev ORANGE from prices
```

```sql type="grid" server="localhost" height="70px" gridConfig={{pager:"-2"}}
select meanBlue:avg BLUE,StandardDeviationBlue:sdev BLUE,meanOrange:avg ORANGE,StandardDeviationOrange:sdev ORANGE from prices
```

## Compounding Returns

Now that we have a series of 12 monthly returns, we can produce the compounded return by multiplying the individual period returns, as long as the returns are expressed as growth rates in what I call "1+R" format.

To compound the returns, all we need to do is add 1 to each return and then multiply them. The result is itself in "1+R" format, so we need to subtract 1.

Let's compute the compounded return of our two series.

```sql type="grid" server="localhost"
1_ratios prices
```

Calculating the product of our returns obtains the yearly return

```sql type="grid" server="localhost" height="70px" gridConfig={{pager:"-2"}}
enlist prd 1_ratios prices
```

## Risk Adjusted Returns

```sql showcodeonly
columnNames:`Month`L0`Lo30`Med40`Hi30`Lo20`Qnt2`Qnt3`Qnt4`Hi20`Lo10`Dec2`Dec3`Dec4`Dec5`Dec6`Dec7`Dec8`Dec9`Hi10;
data:columnNames xcol ("MFFFFFFFFFFFFFFFFFFF";enlist ",") 0: `$":Portfolios_Formed_on_ME_monthly_EW.csv";
adjReturns:`Month`Lo10`Hi10#data;
adjReturns
```

```sql type="grid" server="localhost"
columnNames:`Month`L0`Lo30`Med40`Hi30`Lo20`Qnt2`Qnt3`Qnt4`Hi20`Lo10`Dec2`Dec3`Dec4`Dec5`Dec6`Dec7`Dec8`Dec9`Hi10;
data:columnNames xcol ("MFFFFFFFFFFFFFFFFFFF";enlist ",") 0: `$":Portfolios_Formed_on_ME_monthly_EW.csv";
adjReturns:`Month`Lo10`Hi10#data;
update Lo10*0.01,Hi10*0.01 from adjReturns
```

Let's draw our returns

```sql type="timeseries" server="localhost"
`Month xdesc update Lo10*0.01,Hi10*0.01 from adjReturns
```

Calculating the annualized volatility 

```sql showcodeonly
enlist exec `SmallCap`LargeCap!{sqrt[12]*sdev x}@/:(Lo10;Hi10) from adjReturns
```

```sql type="grid" server="localhost"  height="70px" gridConfig={{pager:"-2",autosizeColumns:false}}
enlist exec `SmallCap`LargeCap!{sqrt[12]*sdev x}@/:(Lo10;Hi10) from adjReturns
```

As well as the annualized return

```sql type="grid" server="localhost"  height="70px" gridConfig={{pager:"-2",autosizeColumns:false}}
`SmallCap`LargeCap xcol enlist 100*-1+(prd 1+0.01*1!adjReturns) xexp (12%count adjReturns)
```

## Computing Maximum Drawdown

Next we will write some code to compute the maximum drawdown of a return series. But before we do so, let's convert our month column to a date column and rename 
our columns

```sql showcodeonly
tbl:`date`smallCap`largeCap xcol update `date$Month from adjReturns
```

```sql type="grid" server="localhost"
tbl:`date`smallCap`largeCap xcol update `date$Month from adjReturns;
tbl
```

## Computing Drawdowns

1. Convert the time series of returns to a time series that represents a wealth index
2. Compute a time series of the previous peaks
3. Compute the Drawdown as the difference between the previous peak and the current value
Let's do this for Large Cap stocks.

```sql showcodeonly
wealthIndex:select date,1000*prds 1+0.01*largeCap from tbl
```
```sql type="line" server="localhost"
wealthIndex:select date,1000*prds 1+0.01*largeCap from tbl;
select date,capital:x from wealthIndex
```

```sql showcodeonly
peaks:select date,maxs 1000*prds 1+0.01*largeCap from tbl
```

```sql type="line" server="localhost"
peaks:select date,maxs 1000*prds 1+0.01*largeCap from tbl;
select date,peaks:x from peaks
```

```sql type="line" server="localhost"
(select date,capital:x from wealthIndex) lj 1!select date,peaks:x from peaks
```

## Displaying Drawdown

```sql type="line" server="localhost"
(![1;wealthIndex]-1!peaks)%1!peaks
```

## Historical Maximum Drawdown

```sql type="grid" server="localhost" height="70px" gridConfig={{pager:"-2",autosizeColumns:false}}
select from ((![1;wealthIndex]-1!peaks)%1!peaks) where x=min x
```

### Maximum Drawdown since 2000

```sql type="grid" server="localhost"  height="70px" gridConfig={{pager:"-2",autosizeColumns:false}} 
select from ((![1;wealthIndex]-1!peaks)%1!peaks) where date>2000.01.01,x=min x
```

##Â Conclusion

This was just a small teaser of what you can do with KDB/Q and Q-Studio Notebooks. 

If you would like to learn more about KDB/Q, check out my blog at www.defconq.tech
